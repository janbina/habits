import org.ajoberstar.grgit.Grgit

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-android-extensions"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.gms.google-services"
    id "androidx.navigation.safeargs.kotlin"
    id "com.github.triplet.play"
}

ext {
    // tag[-commits-hash] (0.0.1-1-g2e87a07)
    git = Grgit.open()

    gitVersionName = git.describe()

    gitVersionCode = 0
    parts1 = gitVersionName.tokenize('.')
    parts2 = parts1[2].tokenize('-')
    gitVersionCode += Integer.parseInt(parts1[0]) * 10000000
    gitVersionCode += Integer.parseInt(parts1[1]) * 100000
    gitVersionCode += Integer.parseInt(parts2[0]) * 1000
    if (parts2.size() > 1) {
        gitVersionCode += Integer.parseInt(parts2[1])
    }
}

play {
    track = 'internal'
    defaultToAppBundles = true
    serviceAccountCredentials = rootProject.file('release/play-account.json')
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.janbina.habits"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode gitVersionCode
        versionName gitVersionName
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file(propOrDef("HABITS_DEBUG_KEYSTORE_FILE", "x"))
            storePassword propOrDef("HABITS_DEBUG_KEYSTORE_PWD", "x")
            keyAlias "habits"
            keyPassword propOrDef("HABITS_DEBUG_KEY_PWD", "x")
        }
        release {
            storeFile rootProject.file("release/app-release.jks")
            storePassword propOrDef("HABITS_RELEASE_KEYSTORE_PWD", "")
            keyAlias "habits"
            keyPassword propOrDef("HABITS_RELEASE_KEY_PWD", "")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug

            buildConfigField "String", "SERVER_CLIENT_ID", propOrDef("HABITS_DEBUG_SERVER_CLIENT_ID", "")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release

            buildConfigField "String", "SERVER_CLIENT_ID", propOrDef("HABITS_SERVER_CLIENT_ID", "")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=com.chibatching.kotpref.preference.dsl.ExperimentalPreferenceScreenDsl",
        ]
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.0.10"

    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.2.0"

    implementation "com.google.dagger:hilt-android:${versions.hilt}"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:${versions.hiltAndroid}"
    kapt "com.google.dagger:hilt-android-compiler:${versions.hilt}"
    kapt "androidx.hilt:hilt-compiler:${versions.hiltAndroid}"

    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation "com.google.android.material:material:1.2.0"

    //implementation "com.github.kizitonwose:CalendarView:0.4.3"
    implementation 'com.github.janbina:CalendarView:3a00a04b8c'

    implementation "com.google.firebase:firebase-core:17.5.0"
    implementation "com.google.firebase:firebase-auth:19.3.2"
    implementation 'com.google.firebase:firebase-firestore-ktx:21.5.0'
    implementation "com.google.android.gms:play-services-auth:18.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1"

    implementation "com.github.kittinunf.result:result:3.0.1"
    implementation "com.github.kittinunf.result:result-coroutines:3.0.1"

    implementation "com.airbnb.android:epoxy:3.11.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    implementation 'com.airbnb.android:mvrx:1.5.1'

    implementation "com.squareup.inject:assisted-inject-annotations-dagger2:0.5.2"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:0.5.2"

    implementation 'com.chibatching.kotpref:kotpref:2.11.0'
    implementation 'com.chibatching.kotpref:livedata-support:2.11.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
    implementation 'com.chibatching.kotpref:preference-screen-dsl:2.11.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
}